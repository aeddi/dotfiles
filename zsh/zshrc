# Needs to be before P10K instant prompt
if [[ `uname` == "Darwin" ]]; then
  source $HOME/.patch_sudo
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block, everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

### Shell customization ###

# Prevent brew from cleaning previous version on upgrade
export HOMEBREW_NO_INSTALL_CLEANUP=1

# Set history & misc. parameters
HISTFILE=$HOME/.zsh_history
HISTSIZE=10000000
SAVEHIST=10000000
setopt   appendhistory     #Append history to the history file (no overwriting)
setopt   sharehistory      #Share history across terminals
setopt   incappendhistory  #Immediately append to the history file, not just when a term is killed
setopt   extendedglob
autoload -U colors && colors

# Ensure permissions are correct for ZSH completion
autoload -U compaudit
[[ -n "$(compaudit)" ]] && compaudit | xargs chmod g-w
if [[ `uname` == "Darwin" && -n "$(compaudit)" ]]; then
  printf "Setting right permissions for compinit - "
  sudo chown -RL root:staff /usr/local/share/zsh
fi

# Enable ZSH completion
autoload -Uz compinit && compinit

# Display CWD as title for xterm* terminals
case $TERM in
  xterm*)
  chpwd() {print -Pn "\e]0;%~\a"}
  ;;
esac


### Zsh plugins ###

ANTIGEN_DIR=$HOME/.antigen
ANTIGEN_BIN=$ANTIGEN_DIR/antigen.zsh

# If antigen is not installed, install it
if [ ! -f $ANTIGEN_BIN ]; then
  echo "Installing antigen!..."
  mkdir -p $ANTIGEN_DIR
  curl -L git.io/antigen --silent > $ANTIGEN_BIN
fi

# Add brew in path for atuin
if [[ `uname` == "Darwin" ]]; then
  export PATH="/opt/homebrew/bin:$PATH"
fi

source $ANTIGEN_BIN

antigen theme romkatv/powerlevel10k
antigen bundle atuinsh/atuin@main
antigen bundle zsh-users/zsh-completions
antigen bundle Tarrasch/zsh-bd

antigen apply

### Includes ###

function include()
{
  if [[ -f $1 ]]; then
    source $1
  else
    printf "$1 is missing\n"
  fi
}

include $HOME/.p10k.zsh
include $HOME/.zshrc_local
include $HOME/.aliases
include $HOME/.functions

unfunction include
